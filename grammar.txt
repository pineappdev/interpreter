-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [TopDef] ;

separator nonempty TopDef "" ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

separator  Arg "," ;

Arg. 	   Arg ::= Type Ident;

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Break.     Stmt ::= "break" ";" ;

Continue.  Stmt ::= "continue" ";" ;

Decl.      Stmt ::= Type [Item] ";" ;

Init.	   Item ::= Ident "=" Expr ;

ItemQIdent. ItemQ ::= Ident ;

ItemQTuple. ItemQ ::= "(" [ItemQ] ")" ;

Unpack.    Stmt ::= "<" ItemQ ">" "=" Expr ";" ;

Ass.       Stmt ::= Ident "=" Expr ";" ;

separator nonempty ItemQ "," ;

separator nonempty Item "," ;

FunDef.    Stmt ::= TopDef ;

Ret.       Stmt ::= "return" Expr ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

IntT.      BaseType ::= "int" ;

StrT.      BaseType ::= "string" ;

BooleanT.  BaseType ::= "bool" ;

BaseT.     Type ::= BaseType ;

ArrayT.    Type ::= Type "[]" ;

TupleT.    Type ::= "Tuple" "<" [Type] ">" ;

separator nonempty Type "," ;

-- Expressions ---------------------------------------------

EVar.      Expr6 ::= Ident ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= Ident "(" [EArg] ")" ;

separator  EArg "," ;

EArgE.     EArg ::= Expr ;

EArgName.  EArg ::= "&" Ident ;

EString.   Expr6 ::= String ;

ETuple.    Expr6 ::= "(" Expr "," [Expr] ")" ;

EArray.    Expr6 ::= "[" [Expr] "]" ;

EGet.      Expr6 ::= Expr6 "[" Expr6 "]" ;

separator  Expr "," ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 6 ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
